/*
 * This source file is part of the Argon project.
 *
 * Licensed under the Apache License v2.0
 *
 * This module provides a comprehensive set of functions for generating random numbers,
 * selecting random elements from sequences, and working with random bytes. It implements
 * various random number generation algorithms, including a system random number generator
 * (if available) and a Mersenne Twister engine.
 *
 * The module offers both low-level functions for direct control over the random number
 * generation process and high-level convenience functions for common random operations.
*/

from "error" import ValueError
from "limits" import UINT_BITS, UINT_MAX, WORDSZ
from "random/_random" import *

if HAVE_SYSRAND {
    pub struct SREngine impl RandomT {
        /*
            Represents a system random number generator engine.

            This struct implements the RandomT trait and uses the system's random number
            generator for producing random values.

            Example:
            engine := SREngine@()
            random_bits := engine.randbits(32)

            See Also:
            - MTEngine: For an alternative random number generator.
        */

        pub func discard(self, z) {
            # Do nothing
        }

        pub func random(self) {
            return UInt::frombytes(sysrand(UINT_BITS // 8)) / UINT_MAX
        }

        pub func randbits(self, k) {
            var bits = WORDSZ * 8

            if k < 0 {
                panic ValueError("k must be greater than zero")
            }

            if k > bits {
                panic ValueError("bits must be between [1,%d)" % bits)
            }

            return UInt::frombytes(sysrand(UINT_BITS // 8)) >> (bits - k)
        }
    }
}

pub func randbelow(engine, n) {
    /*
        Returns a random integer in the range [0, n).

        Example:
        n := 100
        result := randbelow(engine, n)

        - Parameters:
           - engine: Instance of random engine.
           - n: Upper bound of the range (exclusive).
        - Returns: A random UInt in the range [0, n).
    */

    if n == 0 {
        return 0
    }

    var bits = n.bits()
    var rd = engine.randbits(bits)

    loop rd >= n {
        rd = engine.randbits(bits)
    }

    return rd
}

pub func randrange(engine, start, stop, step) {
    /*
        Returns a randomly selected element from the defined range.

        Example:
        result := randrange(engine, 1, 10, 2)

        - Parameters:
           - engine: Instance of random engine.
           - start: An integer specifying the start of the range.
           - stop: An integer specifying the end of the range (exclusive).
           - step: An integer specifying the step between numbers in the range.
        - Returns: A randomly selected UInt from the range.
    */

    if !typeof(start, Int) {
        panic ValueError("invalid non-integer start")
    }

    if !typeof(stop, Int) {
        panic ValueError("invalid non-integer stop")
    }

    if !typeof(step, Int) {
        panic ValueError("invalid non-integer step")
    }

    var width = stop - start

    if step == 1 {
        if width > 0 {
            return start + randbelow(engine, width)
        }

        panic ValueError("empty range for randrange (%d, %d, %d)" % (start, stop, width))
    }

    var n

    switch {
        case step > 0:
            n = (width + step - 1) // step
        case step < 0:
            n = (width + step + 1) // step
        default:
            panic ValueError("zero step for randrange")
    }

    if n <= 0 {
        panic ValueError("empty range for randrange")
    }

    return start + step * randbelow(engine, n)
}

# -------------------- bytes ----------------------

pub func _randbytes(engine, length) {
    /*
        Generate a specified number of random bytes.

        Example:
        random_bytes := _randbytes(engine, 16)

        - Parameters:
           - engine: Instance of random engine.
           - length: Length of the bytes string to create.
        - Returns: Bytes string of random bytes.
    */

    var bts = Bytes(length)

    for var i = 0; i < length; i++ {
        bts[i] = engine.randbits(8)
    }

    return bts
}

# -------------------- sequence -------------------

pub func _randlist(engine, start, stop, step, length) {
    /*
        Returns a list of items randomly selected from the defined range.

        Example:
        random_list := _randlist(engine, 1, 10, 1, 5)

        - Parameters:
           - engine: Instance of random engine.
           - start: An integer specifying the start of the range.
           - stop: An integer specifying the end of the range (exclusive).
           - step: An integer specifying the step between numbers in the range.
           - length: Length of the list to create.
        - Returns: List of items randomly selected.
    */

    var ls = [0] * length

    for var i = 0; i < length; i++ {
        ls[i] = randrange(engine, start, stop, step)
    }

    return ls
}

pub func randchoice(engine, seq) {
    /*
        Choose a random element from a non-empty sequence.

        Example:
        seq := [1, 2, 3, 4, 5]
        chosen := randchoice(engine, seq)

        - Parameters:
           - engine: Instance of random engine.
           - seq: Non-empty sequence.
        - Returns: A random element from the sequence.
    */

    return seq[randbelow(engine, len(seq))]
}

pub func randsample(engine, population, k) {
    /*
        Return a k length list of unique elements chosen from the population sequence.

        Example:
        population := [1, 2, 3, 4, 5]
        sample := randsample(engine, population, 3)

        - Parameters:
           - engine: Instance of random engine.
           - population: Population sequence.
           - k: Length of the list of unique elements.
        - Returns: List of unique elements chosen from the population sequence.
    */

    var plen = len(population)

    if !typeof(k, Int) {
        panic ValueError("invalid non-integer k")
    }

    if k < 0 {
        panic ValueError("k is negative")
    }

    if k > plen {
        panic ValueError("sample larger than population")
    }

    var sample = [nil] * k
    var table = Set()

    for var i = 0; i < k; i++ {
        var index = randbelow(engine, plen)

        loop index in table {
            index = randbelow(engine, plen)
        }

        table.add(index)

        sample[i] = population[index]
    }

    return sample
}

# ---------------- module - defaults --------------

pub let DEFAULT_ENGINE = MTEngine()

pub let choice = randchoice(DEFAULT_ENGINE)
pub let discard = MTEngine::discard(DEFAULT_ENGINE)

pub let randbits = bind(MTEngine::randbits, DEFAULT_ENGINE)
pub let randbytes = _randbytes(DEFAULT_ENGINE)

pub let randint = (a, b) => {
    return randrange(DEFAULT_ENGINE, a, b + 1, 1)
}

pub let random = bind(MTEngine::random, DEFAULT_ENGINE)
pub let randlist = _randlist(DEFAULT_ENGINE)
pub let sample = randsample(DEFAULT_ENGINE)
